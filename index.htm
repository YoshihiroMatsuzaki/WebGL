<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//JP">
<html lang="ja">
<meta charset="utf-8">
<html>
	<head>
		<script type="text/javascript" src="common/math.js"></script>
		<script type="text/javascript" src="common/model.js"></script>
		<script type="text/javascript" src="common/render.js"></script>
		<script type="text/javascript" src="common/drawer.js"></script>
		<script type="text/javascript" src="common/geometry.js"></script>
		<script type="text/javascript" src="draw.js"></script>
		<script type="text/javascript" src="main.js"></script>
		<script type="text" id="vs">
			attribute vec3 vertex;
			uniform   mat4 mvpMatrix;
			uniform   mat4 mMatrix;

			varying   vec3 vVertex;

			void main(void){
				vVertex = (mMatrix * vec4(vertex, 1.0)).xyz;
				gl_Position = mvpMatrix * vec4(vertex, 1.0);
			}
		</script>
		<script type="text" id="fs1">
			#extension GL_OES_standard_derivatives : enable
			precision mediump float;

			varying vec3  vVertex;

			uniform mat4  invMatrix;
			uniform vec3  lightPosition;
			uniform vec3  eyePosition;
			uniform vec4  kd;
			uniform vec4  ka;
			uniform vec4  ks;
			uniform float ns;
			uniform float ior;

			void main(void){
				vec3 L = normalize(invMatrix * vec4(lightPosition - vVertex, 0.0)).xyz;
				vec3 V = normalize(invMatrix * vec4(eyePosition - vVertex, 0.0)).xyz;
				vec3 H = normalize(L + V);
				vec3 dx = dFdx(vVertex);
				vec3 dy = dFdy(vVertex);
				vec3 N  = normalize(cross(normalize(dx), normalize(dy)));
				float diffuse = dot(N, L);
				float specular = pow(dot(H, V), ns);
				gl_FragColor = vec4(diffuse*kd.rgb + specular*ks.rgb + ka.rgb, kd.a);
			}
		</script>
		<script type="text" id="fs">
			#extension GL_OES_standard_derivatives : enable
			precision mediump float;

			varying vec3  vVertex;

			uniform mat4  invMatrix;
			uniform vec3  lightPosition;
			uniform vec3  eyePosition;
			uniform vec4  kd;
			uniform vec4  ka;
			uniform vec4  ks;
			uniform float ns;
			uniform float ior;

			void main(void){
				vec3 L = normalize(invMatrix * vec4(lightPosition - vVertex, 0.0)).xyz;
				vec3 V = normalize(invMatrix * vec4(eyePosition - vVertex, 0.0)).xyz;
				vec3 H = normalize(L + V);
				vec3 dx = dFdx(vVertex);
				vec3 dy = dFdy(vVertex);
				vec3 N  = normalize(cross(normalize(dx), normalize(dy)));
				float nh = max(dot(N, H), 1e-3);
				float nv = max(dot(N, V), 1e-3);
				float nl = max(dot(N, L), 1e-3);
				float hv = max(dot(H, V), 1e-3);
				float G = min(min(2.0*nh*nv / hv, 2.0*nh*nl / hv), 1.0);
				float D = exp((nh*nh - 1.0) / (nh*nh)) / (4.0*nh*nh*nh*nh);
				float g = sqrt(ior*ior + hv*hv - 1.0);
				float g_phv = g + hv;
				float g_mhv = g - hv;
				float F = ((g_phv*hv - 1.0)*(g_phv*hv - 1.0) / ((g_mhv*hv - 1.0)*(g_mhv*hv - 1.0)) + 1.0) * g_mhv*g_mhv / (g_phv*g_phv);
				float specular = max(G*D*F / (4.0*nl*nv), 0.0);
				float diffuse = max(nl - F, 0.0);
				gl_FragColor = vec4(diffuse*kd.rgb + specular*ks.rgb + ka.rgb, kd.a);
			}
		</script>
	</head>
	<body>
		<table>
			<tr>
				<td><canvas id="canvas"></canvas></td>
				<td><span id="disp"></span><br/>スケール<br><input id="scale" type="range" value="100" min="1" max="5000" style="width:256px;"></td>
			</tr>
		</table>
	</body>
</html>
